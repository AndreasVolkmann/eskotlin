/*
 * Copyright (c) 2016. Michael Buhot m.buhot@gmail.com
 */

group 'mbuhot'
version '0.1.0'

apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.0.0"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
    }
}

dependencies {
    compile "org.elasticsearch:elasticsearch:2.2.0"
    compile "org.jetbrains.kotlin:kotlin-stdlib:1.0.0"
    testCompile 'com.fasterxml.jackson.core:jackson-databind:2.7.2'
    testCompile "junit:junit:4.11"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// add source jar tasks as artifacts
artifacts {
    archives sourcesJar
}

repositories {
    mavenCentral()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

bintray {
    user = bintray_user //this usually comes from gradle.properties file in ~/.gradle
    key = bintray_api_key //this usually comes from gradle.properties file in ~/.gradle
    publications = ['mavenJava']
    dryRun = false
    publish = true
    pkg {
        repo = 'maven'
        name = 'eskotlin'
        desc = 'Elasticsearch Query DSL for Kotlin'
        websiteUrl = 'https://github.com/mbuhot/eskotlin'
        issueTrackerUrl = 'https://github.com/mbuhot/eskotlin/issues'
        vcsUrl = 'https://github.com/mbuhot/eskotlin.git'
        licenses = ['MIT']
        labels = ['kotlin', 'elasticseasrch']
        publicDownloadNumbers = true
        version {
            name = project.version //Bintray logical version name
            released  = new Date() //2 possible values: date in the format of 'yyyy-MM-dd'T'HH:mm:ss.SSSZZ' OR a java.util.Date instance
            vcsTag = project.version
            mavenCentralSync {
                sync = false //Optional (true by default). Determines whether to sync the version to Maven Central.
            }
        }
    }
}


